# antlr-ish reference for grammar
#
# 0 or more times: ( )*
# 1 or more times: ( )+
# 0 or 1 times: ( )?


file = ( statement )*
# NOTE: Statements cannot begin with a dictionary literal. See bottom of file.
statement = assignment | if | while | for | rule | ( ">" )? expression ";" | block | functionDef

functionDef = "function" "(" functionDefArgList ")" block
functionDefArgList = ( name ( "*" )? ( "," name ( "*" )? )* )?

# Variables
assignment = name "=" expression ";"

# Control structures.
if = "if" expression block ( "else" "if" expression block )* ( "else" block )?
while = "while" expression block
for = "for" "(" assignment ";" expression ";" assignment ")" block
block = "{" ( statement )* "}"

# Zero or more expressions, separated by commas.
expressionList = ( expression ( "," expression )* )?

# Makefile-style rules
rule = expression ( "," expression )* ":" expressionList block

# Arrays and dictionaries
arrayLiteral = "[" expressionList ( "," )? "]"
dictLiteral = "{" ( keyValuePair ( "," keyValuePair )* )? ( "," )? "}"
keyValuePair = expression ":" expression

# The following is roughly equivalent to:
#   expression = binary | unary | primary | arrayLiteral | dictLiteral | "(" expression ")" | functionCall | literal
#   binary = expression ( "*" | "/" | "-" | "+" | "==" | "!=" | "<" | "<=" | ">" | ">=" ) expression
#   unary = ( "!" | "-" ) expression
#   functionCall = ( name | "(" expression ")" ) "(" expressionList ")"
#	literal = NUMBER | STRING | "true" | "false"
#
# However, that grammar is ambiguous, and also doesn't encode associativity and order of
# operations. That formulation also doesn't account for non-chainable binary operators.

expression = and ( "||" and )*
and = equality ( "&&" equality )*
equality = comparison ( ( "==" | "!=" ) comparison )? # Can't be chained 
comparison = addSub ( ( "<" | "<=" | ">" | ">=" ) addSub )? # Can't be chained
addSub = multDiv ( ( "-" | "+" ) multDiv )*
multDiv = unary ( ( "*" | "/" ) unary )*
unary = ( ( "!" | "-" ) unary ) | primary
primary = literal | "(" expression ")" ( index | args )? | name ( index | args )? | lambda
literal = NUMBER | STRING | "true" | "false" | arrayLiteral | dictLiteral
args = "(" expressionList ")"
index = ( "[" expression "]" )+
lambda = ( name | "(" ( name ( "," name )* )? ")" ) "->" ( expression | block )

# If a statement is a block, then a line starting with "{" is ambiguous. It could be
# a block, or it could be a literal dictionary. The lookahead is very expensive and complex.
# Instead, we're copying what Javascript does, and just make starting a line with an object
# literal illegal.
